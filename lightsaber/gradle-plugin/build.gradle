import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply from: "$rootDir/pablo.gradle"

targetCompatibility = javaVersion
sourceCompatibility = javaVersion

dependencies {
  relocate project(':processor')
  implementation "org.jetbrains.kotlin:kotlin-stdlib$kotlinRuntimeVariant:$kotlinVersion"

  compileOnly gradleApi()
  compileOnly "com.android.tools.build:gradle:$androidToolsVersion"
  compileOnly "com.android.tools.build:gradle-api:$androidToolsVersion"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = javaVersion
    apiVersion = kotlinLanguageVersion
    languageVersion = kotlinLanguageVersion
  }
}

def generatedDir = new File(projectDir, "generated")
def generatedJavaSourcesDir = new File(generatedDir, "main/java")

task generateBuildClass {
  inputs.property('version', version)
  outputs.dir generatedJavaSourcesDir

  doLast {
    def buildClassFile = new File(generatedJavaSourcesDir, "com/joom/lightsaber/plugin/Build.java")
    buildClassFile.parentFile.mkdirs()
    buildClassFile.text = "" +
        "package com.joom.lightsaber.plugin;\n" +
        "\n" +
        "public class Build {\n" +
        "    public static final String VERSION = \"$version\";\n" +
        "}\n"
  }
}

sourceSets {
  main {
    java.srcDirs += generatedJavaSourcesDir
  }
}

tasks.matching { it instanceof AbstractCompile || it instanceof KotlinCompile }.forEach {
  it.dependsOn tasks.generateBuildClass
}

project.afterEvaluate {
  tasks.named(sourceSets.main.getSourcesJarTaskName()).configure {
    it.dependsOn tasks.generateBuildClass
  }
}

clean.doFirst {
  delete generatedDir
}

idea {
  module {
    generatedSourceDirs += generatedJavaSourcesDir
  }
}

pablo {
  shadow {
    relocate 'com.joom.lightsaber'
  }
}
